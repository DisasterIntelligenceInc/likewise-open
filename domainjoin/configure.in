#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(centeris-domainjoin, 1.0, support@centeris.com)
AC_CONFIG_SRCDIR([include/DomainJoinConfig.h.in])
AC_CONFIG_HEADER([include/DomainJoinConfig.h])
AC_PREFIX_DEFAULT(/usr/centeris)

RM='/bin/rm -f'
AC_SUBST(RM)

LINK='/bin/ln -s'
AC_SUBST(LINK)

if test -z "${AR}"; then
AC_CHECK_PROGS(AR, ar, gar, ar_was_not_found)
fi

LDSHFLAGS="${LDSHFLAGS} -shared -fPIC"

libxml2_dir=/usr/centeris
AC_ARG_WITH(libxml2_dir,
[ --with-libxml2-dir=DIR        Specify libxml2 XML library location ],
[ case "$withval" in
none)
	AC_MSG_RESULT(no libxml2 path given)
	;;
*)
	libxml2_dir=$withval;
	AC_MSG_RESULT(using libxml2 libraries at $withval)
	;;
esac
])

LIBXML2_CPPFLAGS="-I$libxml2_dir/include -I$libxml2_dir/include/libxml2"
if test -e $libxml2_dir/lib64 ; then
LIBXML2_LIBDIR="$libxml2_dir/lib64"
else
LIBXML2_LIBDIR="$libxml2_dir/lib"
fi
LIBXML2_LDFLAGS="-L$LIBXML2_LIBDIR"
#libxml2 requires -lm
LIBXML2_LDFLAGS="$LIBXML2_LDFLAGS -lxml2 -lm"

AC_SUBST(LIBXML2_CPPFLAGS)
AC_SUBST(LIBXML2_LDFLAGS)


centutils_dir=/usr/centeris
AC_ARG_WITH(centutils_dir,
[ --with-centutils-dir=DIR        Specify centutils library location ],
[ case "$withval" in
none)
	AC_MSG_RESULT(no centutils path given)
	;;
*)
	centutils_dir=$withval;
	AC_MSG_RESULT(using centutils libraries at $withval)
	;;
esac
])

CENTUTILS_CPPFLAGS="-I$centutils_dir/include -I$centutils_dir/include"
if test -e $centutils_dir/lib64 ; then
	CENTUTILS_LIBDIR="$centutils_dir/lib64"
else
	CENTUTILS_LIBDIR="$centutils_dir/lib"
fi
CENTUTILS_LDFLAGS="-L$CENTUTILS_LIBDIR"
CENTUTILS_LIBS="-lcentutils -lgpglib"

AC_SUBST(CENTUTILS_CPPFLAGS)
AC_SUBST(CENTUTILS_LDFLAGS)
AC_SUBST(CENTUTILS_LIBS)

CPPFLAGS="$CPPFLAGS -D_REENTRANT -fPIC"
LIBVER_MAJOR=1
SHLIBEXT=.so
SHLIBVEXT=.so.$LIBVER_MAJOR
DYNLIBEXT=.so
DYNLIBVEXT=.so.$LIBVER_MAJOR
LWI_OS_FLAGS=""
LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS=""
case `uname -s` in
    AIX)
	LWI_OS_FLAGS="-D__LWI_AIX__"
	LDFLAGS="${LDFLAGS} -static-libgcc"
	BUILD_SHLIB_CMD='$(CC) -shared -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-o$@'
	BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
	LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    Darwin)
        LWI_OS_FLAGS="-D__LWI_MACOSX__"
        SHLIBEXT=.dylib
        SHLIBVEXT=.$LIBVER_MAJOR.dylib
        LDFLAGS="-headerpad_max_install_names $LDFLAGS"
        BUILD_DYNLIB_CMD='$(CC) -bundle $(CFLAGS) -o $@ $(OBJS) $(LDFLAGS) $(LIBS)'
        BUILD_SHLIB_CMD='$(CC) -undefined error -dynamiclib -compatibility_version $(LIBVER_MAJOR) -current_version $(LIBVER_MAJOR) -install_name "lib$(LIBNAME)$(SHLIBVEXT)" -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS)'
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_mac.o"
        ;;

    Linux)
        LWI_OS_FLAGS="-D__LWI_LINUX__"
        BUILD_SHLIB_CMD='$(CC) -shared -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-h$@'
        BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    SunOS)
        LWI_OS_FLAGS="-D__LWI_SOLARIS__"
        LDFLAGS="${LDFLAGS} -static-libgcc"
        BUILD_SHLIB_CMD='$(CC) -shared -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-o$@'
        BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    HP-UX)
        LWI_OS_FLAGS="-D__LWI_HP_UX__"
        if test `uname -m` != "ia64"; then
           SHLIBEXT=.sl
           SHLIBVEXT=.sl.$LIBVER_MAJOR
           DYNLIBEXT=.sl
           DYNLIBVEXT=.sl.$LIBVER_MAJOR
        fi
        BUILD_SHLIB_CMD='$(CC) -shared -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-h$@'
        BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    *)
        AC_MSG_ERROR(OS not supported, 1)
        ;;
esac

AC_SUBST(SHLIBEXT)
AC_SUBST(SHLIBVEXT)
AC_SUBST(DYNLIBEXT)
AC_SUBST(DYNLIBVEXT)
AC_SUBST(LIBVER_MAJOR)
AC_SUBST(BUILD_SHLIB_CMD)
AC_SUBST(BUILD_DYNLIB_CMD)
AC_SUBST(LWI_OS_FLAGS)
AC_SUBST(LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS)

AC_ARG_ENABLE(debug,
[ --enable-debug enable debugging ],
CFLAGS="${CFLAGS} -g"
)

if test "x$CFLAGS" = "x"; then
    CFLAGS="-O2"
fi

CFLAGS="${CFLAGS} -I\$(BUILD_ROOT)/../centutils/libcentutils"
CFLAGS="${CFLAGS} -I\$(BUILD_ROOT)/../centutils/libgpglib"

LDFLAGS="${LDFLAGS} -L\$(BUILD_ROOT)/../centutils/libcentutils"
LDFLAGS="${LDFLAGS} -L\$(BUILD_ROOT)/../centutils/libgpglib"



AC_SUBST(LDSHFLAGS)

if test "x$prefix" = "xNONE"; then
AC_DEFINE_UNQUOTED(PREFIXDIR, "$ac_default_prefix", [The directory that contains centeris/bin])
else
AC_DEFINE_UNQUOTED(PREFIXDIR, "$prefix", [The directory that contains centeris/bin])
fi

AC_DEFINE_UNQUOTED(SAMBACONFDIR, "/etc/samba", [The directory that stores lwiauthd.conf])

AC_C_BIGENDIAN([AC_DEFINE(WORDS_BIGENDIAN, 1, [Define to 1 if the system uses bigendian byte ordering])],[AC_DEFINE(WORDS_LITTLEENDIAN, 1, [Define to 1 if the system uses little-endian byte ordering])],[AC_DEFINE(WORDS_UNKNOWN, 1, [Define to 1 if the byte ordering of the system is unknown])])

AC_MSG_CHECKING([LDFLAGS])
AC_MSG_RESULT($LDFLAGS)
# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

# Check for compiler options
AC_MSG_CHECKING([if linker supports -rpath-link])
saved_LDFLAGS="$LDFLAGS"
LDFLAGS="$LDFLAGS -Wl,-rpath-link=/usr/lib"

AC_TRY_LINK([], [],
      [ AC_MSG_RESULT([yes]); RPATH_LINK=yes ],
      [ AC_MSG_RESULT([no]); RPATH_LINK=no ])

LDFLAGS="$saved_LDFLAGS"

if test "x$RPATH_LINK" = "xyes"
then
	LDFLAGS="$LDFLAGS -Wl,-rpath-link,$LIBXML2_LIBDIR"
else
	LDFLAGS="$LDFLAGS -L$LIBXML2_LIBDIR"
fi

# Checks for libraries.

AC_CHECK_LIB([nsl], [gethostname])
AC_CHECK_LIB([resolv], [res_query])
AC_CHECK_LIB([pthread], [pthread_create])
AC_CHECK_LIB([rt], [clock_settime])
AC_CHECK_LIB([socket], [bind])
AC_CHECK_LIB([dl], [dlopen])

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([arpa/inet.h fcntl.h limits.h netdb.h netinet/in.h stdlib.h string sys/socket.h syslog.h unistd.h sys/types.h fcntl.h sys/stat.h sys/systeminfo.h time.h sys/time.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_HEADER_TIME
AC_STRUCT_TM

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([atexit bzero localtime_r memset select socket strchr strerror vsyslog sysinfo sysconf clock_gettime clock_settime settimeofday gettimeofday sigprocmask])
AC_CHECK_DECLS([isblank], [], [], [#include <ctype.h>])

AC_ARG_WITH(gtk,
[ --with-gtk=yes/no     Enable/disable GTK support (default: yes) ],
[ case "$withval" in
no)
	WANT_GTK=no
	;;
*)
	WANT_GTK=yes
	;;
esac
])

if test "x$WANT_GTK" = "xyes"
then
	PKG_CHECK_MODULES(GTK, gtk+-2.0 libglade-2.0 gthread-2.0,have_gtk=yes,have_gtk=no)
fi

if test "x$have_gtk" = "xyes"
then
	GTK_SUBDIR="domainjoin-gui/gtk"
else
	GTK_SUBDIR=""
fi

AC_SUBST(GTK_SUBDIR)

dnl Autoconf will define bindir to '${exec_prefix}/bin' by default.
dnl exec_prefix is then defined to '${prefix}'. Typically make would
dnl recursively substitute all of the variables, however if bindir is used
dnl in a shell script, the variables must be pre-expanded in autoconf. The
dnl expansion in run from AC_CONFIG_COMMANDS_PRE so that ${prefix} is defined
dnl before the expansion is run.
m4_foreach([name], [[bindir], [libexecdir]], [dnl
    AC_CONFIG_COMMANDS_PRE([
expanded_]name[="$]name["
while test "$expanded_]name[" != `eval "echo \"$expanded_]name[\""` ; do
        eval "expanded_]name[=\"$expanded_]name[\""
done
]   )
    AC_SUBST([expanded_]name)
])

AC_CONFIG_FILES([Makefile
                 libdomainjoin/src/Makefile
                 domainjoin-cli/src/Makefile
		 domainjoin-gui/gtk/Makefile
		 scripts/ConfigureLogin
		 scripts/gpcron
		 scripts/domainjoin-cli
                 tests/Makefile
                 tests/test_auth_conf/src/Makefile
                 tests/test_proc_utils/src/Makefile
                 tests/test_nsswitch_change/src/Makefile
                 tests/test_dhcpname_change/src/Makefile
                 tests/test_sec_user/src/Makefile
                 tests/test_login_cfg/src/Makefile
                 tests/test_file_utils/src/Makefile], echo timestamp > stamp-h
)

case `uname -s` in
    Darwin)
          AC_CONFIG_FILES([domainjoin-gui/carbon/libdomainjoin-mac/src/Makefile], echo timestamp >> stamp-h)
          LIBDOMAINJOINMAC_SUBDIR="domainjoin-gui/carbon/libdomainjoin-mac/src"
          ;;
    *)
          LIBDOMAINJOINMAC_SUBDIR=""
          ;;
esac

AC_SUBST(LIBDOMAINJOINMAC_SUBDIR)

AC_OUTPUT
